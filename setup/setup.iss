; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "MeFtpSyncWrapper"
#define MyAppVersion "1.0.0.1"
#define MyAppPublisher "Me"
#define MyAppURL "http://jowalva.ddns.net/"
#define MyCustomer "Me"
#define MyCompany "Me"
#define Console "CFTPSyncWrapper.exe"
#define Winform "WFTPSyncWrapper.exe"
#define Common "MeCommon.dll"
#define Wrapper "WCFTPSyncWrapper.dll"
#define LogFolder "logs"
#define Media "Media"
#define FTPSync "FTPSync"
#define ShortCutIcon "%SystemRoot%\system32\shell32.dll"
#define OutPutDir "Setup"


#define Mode "Release"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{05FD5A57-C7B6-4E8E-AB86-BC7C046E0930}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyCustomer}\{#MyAppName}
DefaultGroupName={#MyAppName}
LicenseFile=..\LICENSE.MD
OutputDir=..\..\bin\{#OutPutDir}
OutputBaseFilename={#MyAppName}Setup-{#Mode}-{#MyAppVersion}
SetupIconFile="..\Medias\setup.ico"
Compression=lzma
SolidCompression=yes
VersionInfoCopyright=Copyright © ME 2016
VersionInfoCompany= {#MyCompany}

#include <idp.iss>

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Dirs]
Name: "{app}\{#LogFolder}"
Name: "{app}\{#Media}"
Name: "{app}\{#FTPSync}"

[Files]
Source: "unzipper.dll"; Flags: dontcopy
Source: "..\..\bin\{#Mode}\{#Console}"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\{#Console}.config"; DestDir: "{app}"; Flags: onlyifdoesntexist uninsneveruninstall confirmoverwrite;
Source: "..\..\bin\{#Mode}\{#WinForm}"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\{#WinForm}.config"; DestDir: "{app}"; Flags: onlyifdoesntexist uninsneveruninstall confirmoverwrite;
Source: "..\..\bin\{#Mode}\{#Common}"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\{#Wrapper}"; DestDir: "{app}"; Flags: ignoreversion;

Source: "..\..\bin\{#Mode}\log4net.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\log4net.xml"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\CommandLine.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\CommandLine.xml"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\MadMilkman.Ini.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "..\..\bin\{#Mode}\MadMilkman.Ini.xml"; DestDir: "{app}"; Flags: ignoreversion;

Source: "..\ReadMe.md"; DestDir: "{app}"
Source: "..\ChangeLog.md"; DestDir: "{app}"
Source: "..\LICENSE.MD"; DestDir: "{app}"
Source: "..\Medias\favicon.ico"; DestDir: "{app}\{#Media}"
Source: "..\Medias\favicon.ico"; DestDir: "{app}"
Source: "..\dist\seedbox.ini"; DestDir: "{app}\{#FTPSync}"; Flags: onlyifdoesntexist uninsneveruninstall confirmoverwrite;

[Run]
Filename: "http://www.cyberkiko.com/page/ftpsync/"; Flags: shellexec runasoriginaluser postinstall; Description: "Open the FTPSync url."

[UninstallDelete]
Type: filesandordirs; Name: "{app}\logs"
Type: filesandordirs; Name: "{app}\FTPSync\*.exe"
Type: filesandordirs; Name: "{app}\FTPSync\*.dll"
Type: filesandordirs; Name: "{app}\FTPSync\*.bat"
Type: filesandordirs; Name: "{app}\FTPSync\*.txt"
Type: filesandordirs; Name: "{app}\FTPSync\*.log"
Type: filesandordirs; Name: "{app}\FTPSync\demo*.*"
Type: filesandordirs; Name: "{app}\FTPSync\Branding"
Type: filesandordirs; Name: "{app}\FTPSync\Docs"


[Code]
procedure InitializeWizard();
begin
  idpAddFile('http://cdn.cyberkiko.com/Download/Tools/FTPSync.zip', ExpandConstant('{tmp}\FTPSync.zip'));

  idpDownloadAfter(wpReady);
end;

procedure unzip(src, target: AnsiString);
external 'unzip@files:unzipper.dll stdcall delayload';

procedure CurStepChanged(CurStep: TSetupStep);
begin
    if CurStep = ssPostInstall then 
    begin
      unzip(ExpandConstant('{tmp}\FTPSync.zip'), ExpandConstant('{app}\FTPSync'));
    end;
end;

